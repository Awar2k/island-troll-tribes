library TradeShipRoute requires TradeShipStop, TradeShipSpawnOrder, IDUtils {

  constant real    TRADE_SHIP_PAUSE_TIME_DEST = 0;
  constant real    TRADE_SHIP_PAUSE_TIME_SELL = 0;

  constant integer STATE_MOVING = 1;
  constant integer STATE_STOPPED = 2;

  public struct TradeShipRoute {
    static integer length = 0;
    private static trigger whenEnterRegion;
    private static trigger whenSellItem;
    private static Table   routes;
    private static region  stopsAtRegions;

    private unit          tradeShip;
    private TradeShipStop nextStop;
    private TradeShipStop firstStop;
    private timer         myTimer;
    private integer       state = STATE_STOPPED;
    private TradeShipIdListNode nextShip;

    private static method onInit() {
      stopsAtRegions = CreateRegion();
      whenEnterRegion = CreateTrigger();
      TriggerRegisterEnterRegion( whenEnterRegion, stopsAtRegions, null );
      TriggerAddAction( whenEnterRegion, static method TradeShipRoute.relayEnterRegionToRoute );

      whenSellItem = CreateTrigger();
      TriggerRegisterPlayerUnitEvent( whenSellItem, Player( 15 ), EVENT_PLAYER_UNIT_SELL_ITEM, null );
      TriggerAddAction( whenSellItem, static method TradeShipRoute.relaySellItemToRoute );

      routes = Table.create();
    }

    static method existsForUnit( unit u ) -> boolean {
      return routes.has_h( u );
    }

    static method findByShip( unit u ) -> thistype {
      return thistype( routes.integer_h[u] );
    }

    static method operator[] (integer i) -> thistype {
      return thistype( i );
    }

    static method addRect( rect r ) {
      RegionAddRect( stopsAtRegions, r );
    }

    static method create() -> thistype {
      thistype self = thistype.allocate();
      thistype.length += 1;
      self.nextShip = GetTradeShipSpawnOrder();
      self.tradeShip = null;
      self.firstStop = -1;
      self.myTimer = NewTimerEx( self );
      return self;
    }

    private method onDestroy() {
      thistype.length -= 1;
      stopsAtRegions = null;
    }

    method setFirstStop( TradeShipStop stop ) -> thistype {
      firstStop = stop;
      return this;
    }

    method createNextShip() -> thistype {
      tradeShip = CreateUnit( Player( 15 ), nextShip.getId(), firstStop.x, firstStop.y, 0 );
      nextShip = nextShip.getNext();
      routes.integer_h[tradeShip] = this;
      nextStop = firstStop.next;
      state = STATE_STOPPED;
      return this;
    }

    method removeShip() -> thistype {
      routes.remove_h( tradeShip );
      RemoveUnit( tradeShip );
      tradeShip = null;
      return this;
    }

    method getShip() -> unit {
      return tradeShip;
    }

    method moveToNextStop() -> thistype {
      if ( state != STATE_MOVING ) {
        PauseTimer( myTimer );
        state = STATE_MOVING;

        IssuePointOrder( tradeShip, "move", nextStop.x, nextStop.y );

        TimerStart( myTimer, 1.0, true, function() {
          TradeShipRoute this = TradeShipRoute( GetTimerData( GetExpiredTimer() ) );
          IssuePointOrder( tradeShip, "move", nextStop.x, nextStop.y );
        });
      }

      return this;
    }

    method pause( real duration ) -> thistype {
      if ( state != STATE_STOPPED ) {
        PauseTimer( myTimer );
        state = STATE_STOPPED;
        IssueImmediateOrder( tradeShip, "holdposition" );

        TimerStart( myTimer, duration, false, function() {
          TradeShipRoute( GetTimerData( GetExpiredTimer() ) ).moveToNextStop();
        });
      }

      return this;
    }

    private method processEnterRegion() {
      if ( nextStop.containsShip(tradeShip) ) {
        PauseTimer( myTimer );

        if ( nextStop.isEndOfRoute() ) {
          removeShip().createNextShip().moveToNextStop();
        } else {
          nextStop = nextStop.next;
          pause( TRADE_SHIP_PAUSE_TIME_DEST );
        }
      }
    }

    static method relaySellItemToRoute() {
      unit u = GetTriggerUnit();
      if ( existsForUnit( u ) ) {
        TradeShipRoute.findByShip( u ).pause( TRADE_SHIP_PAUSE_TIME_SELL );
      }
      u = null;
    }

    static method relayEnterRegionToRoute() {
      unit u = GetEnteringUnit();
      if ( existsForUnit( u ) ) {
        TradeShipRoute.findByShip( u ).processEnterRegion();
      }
      u = null;
    }

  }
}