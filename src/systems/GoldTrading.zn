library GoldTrading requires ChatCommands, PublicLibrary, Utils {
	function GiveGold(ArgsList a) {
		integer playerId = S2I(a[0]) - 1;
		integer amount = S2I(a[1]);
		integer tribeId = GetPlayerTribeId(a.triggerPlayer);
		integer currentGold;
		integer theirGold;
		player target;

		if (!udg_STARTED) {
			return;
		}

		if (playerId < 0 || playerId > 11) {
			playerId = ParsePlayerIdOnTribe(a[0], tribeId);
		}

		if (playerId < 0 || playerId > 11) {
			DisplayTextToPlayer(a.triggerPlayer, 0, 0, RED_COLOR + "Command is '-g (player name or id) (amount)'|r");
			return;
		}

		if (playerId == GetPlayerId(a.triggerPlayer)) {
			DisplayTextToPlayer(a.triggerPlayer, 0, 0, RED_COLOR + "You cannot trade with yourself|r");
			return;
		}

		if (GetPidTribeId(playerId) != tribeId) {
			DisplayTextToPlayer(a.triggerPlayer, 0, 0, RED_COLOR + "You cannot trade with that player|r");
			return;
		}

		if (amount < 0) {
			DisplayTextToPlayer(a.triggerPlayer, 0, 0, RED_COLOR + "Amount must be greater than zero|r");
			return;
		}

		currentGold = GetPlayerState(a.triggerPlayer, PLAYER_STATE_RESOURCE_LUMBER);
		if (amount > currentGold) {
			DisplayTextToPlayer(a.triggerPlayer, 0, 0, RED_COLOR + "You don't have that much gold!|r");
			return;
		}

		target = Player(playerId);

		SetPlayerState(a.triggerPlayer, PLAYER_STATE_RESOURCE_LUMBER, currentGold - amount);

		theirGold = GetPlayerState(target, PLAYER_STATE_RESOURCE_LUMBER);
		SetPlayerState(target, PLAYER_STATE_RESOURCE_LUMBER, theirGold + amount);

		DisplayTextToPlayer(target, 0, 0, GENERAL_COLOR + "You received " + GOLD_COLOR + a[1] + " gold|r" + GENERAL_COLOR + " from " + COLOR_CODE[GetPlayerId(a.triggerPlayer)] + GetPlayerName(a.triggerPlayer) + "|r");
		DisplayTextToPlayer(a.triggerPlayer, 0, 0, GENERAL_COLOR + "Sent " + GOLD_COLOR + a[1] + " gold|r" + GENERAL_COLOR + " to " + COLOR_CODE[GetPlayerId(target)] + GetPlayerName(target) + "|r");

		target = null;
	}

	function onInit() {
		ChatCommands.registerArgFunc(null, "gold", GiveGold);
		ChatCommands.registerArgFunc(null, "g", GiveGold);
	}
}