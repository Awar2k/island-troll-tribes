library ForcedDuel requires GameConfig, PublicLibrary, Map {
  timerdialog td;

  function startDuel() {
    integer i;
    unit u;

    ReleaseTimer(GetExpiredTimer());
    TimerDialogDisplay(td, false);
    DestroyTimerDialog(td);

    for (0 <= i < PLAYER_NEUTRAL_AGGRESSIVE) {
      u = GetPlayerTroll(Player(i));
      if (IsUnitAliveBJ(u)) {
        PauseUnit(u, false);
      }
    }
  }

  function moveTrollsToArena() {
    timer t = NewTimer();
    player p;
    location l;
    integer i;
    integer j;
    unit u;
    integer numPlayersPerTribe = GameConfig.getInstance().getNumPlayersPerTribe();
    integer numTribes = GameConfig.getInstance().getNumTribes();

    ReleaseTimer(GetExpiredTimer());

    for (0 <= i < numTribes) {
      l = GetRandomLocInRect(gg_rct_Dualing_Area);
      for (0 <= j < numPlayersPerTribe) {
        p = Player(j + i * numPlayersPerTribe);
        u = GetPlayerTroll(p);
        if (IsUnitAliveBJ(u)) {
          SetUnitPositionLoc(u, l);
          PauseUnit(u, true);
          SetUnitLifePercentBJ(u, 100);
          SetUnitManaPercentBJ(u, 100);
          SetPlayerState(p, PLAYER_STATE_RESOURCE_GOLD, udg_MAX_HEAT);
          SetCameraPositionLocForPlayer(p, l);
        }
      }
    }

    TimerStart(t, 3.0, false, function startDuel);
    TimerDialogSetTitle(td, "Duel begins in");
    DisplayTextToPlayer(GetLocalPlayer(), 0, 0, GENERAL_COLOR + "Your duel has begun!|r");

    t = null;
    p = null;
    l = null;
    u = null;
  }

  function startDuelCountdown(real numSeconds) {
    timer t = NewTimer();
    td = CreateTimerDialog(t);

    TimerStart(t, numSeconds, false, function moveTrollsToArena);
    TimerDialogSetTitle(td, "Duel in");
    TimerDialogDisplay(td, true);

    t = null;
  }

  public function onGameStart() {
    if (GameConfig.getInstance().isForcedDuelEnabled()) {
      startDuelCountdown(GameConfig.getInstance().getForcedDuelAfter() * 60.0);
    }
  }

  function onInit() {
    Map.onGameStart(function onGameStart);
  }
}
