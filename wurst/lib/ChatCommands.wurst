package ChatCommands
import LinkedList
import HashMap
import StringUtils

let maxPlayers = PLAYER_NEUTRAL_AGGRESSIVE - 1

interface CommandHandler
    function run(player triggerPlayer, LinkedList<string> arguments)

let commands = new HashMap<player, HashMap<string, CommandHandler>>()

function registerCommand(player thePlayer, string command, CommandHandler handler)
    commands.get(thePlayer).put(command, handler)

function registerCommandAll(string command, CommandHandler handler)
    for i = 0 to maxPlayers
        registerCommand(players[i], command, handler)

function deregisterCommand(player thePlayer, string command)
    commands.get(thePlayer).remove(command)

function deregisterCommandAll(string command)
    for i = 0 to maxPlayers
        deregisterCommand(players[i], command)

init
    let t = CreateTrigger()

    for i = 0 to maxPlayers
        commands.put(players[i], new HashMap<string, CommandHandler>())
        t.registerPlayerChatEvent(players[i], "-", false)

    t.addAction() ->
        let triggerPlayer = GetTriggerPlayer()
        let playerCommands = commands.get(triggerPlayer)
        let tokens = GetEventPlayerChatString().substring(1).split(" ")
        let command = tokens.get(0)
        if playerCommands.has(command)
            playerCommands.get(command).run(triggerPlayer, tokens)

    registerCommandAll("test") (triggerPlayer, args) ->
        args.forEach(i -> print(i))
